# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0
PortGroup           makefile 1.0

github.setup        vlang v 0.3.1
github.tarball_from archive
revision            0

name                vlang
# Remove weekly from version number
#version             [string range ${version} 7 end]
homepage            https://vlang.io/

description         Simple, fast, safe, compiled language for developing maintainable software
long_description    {*}${description}. Compiles itself in <1s with zero library dependencies. This port \
                    provides weekly builds of Vlang.

license             MIT
categories          lang
maintainers         {harens @harens} openmaintainer

# Install the V compiler as a separate distfile.
# Based on https://github.com/macports/macports-ports/blob/master/games/minetest/Portfile
set vc_commit       119e6752e94814715dbb926d438027a848d5be2c
set main_distfile   ${distfiles}
set vc_distfile     ${vc_commit}${extract.suffix}
set vc_mastersite   https://github.com/vlang/vc/archive

distfiles           ${main_distfile}:main \
                    ${vc_distfile}:vc

master_sites        ${github.master_sites}:main \
                    ${vc_mastersite}:vc

checksums           ${main_distfile} \
                    rmd160  225ca148b2818c40290bfeba074c5e61b3c272cd \
                    sha256  a2ae237d70ac2e17ccd6d26505ccce66f92b17d76f4079192c743bc443f2d0b7 \
                    size    4575858 \
                    ${vc_distfile} \
                    rmd160  4b4259b870f076111f2772aaec6e3844a14838c3 \
                    sha256  bd5589c4133d79e7c4b46e2ee3679e4745e4cec972337eec3598076ffe087400 \
                    size    1488873

# V uses MacPorts LegacySupport natively within its Makefile
set legacy_version 15
set legacysupport_version 1.0.6
if { ${os.platform} eq "darwin" && ${os.major} <= ${legacy_version} } {
    master_sites-append       https://github.com/macports/macports-legacy-support/archive:legacy
    distfiles-append          v${legacysupport_version}${extract.suffix}:legacy

    checksums-append    v${legacysupport_version}${extract.suffix} \
                            rmd160  4a312d6afd92d1bc5fb8863bbc18c626329a77e1 \
                            sha256  f38cafc931328453ce1d2ff5fc0bc2b39207b17a5ed29623d4bee46523a961b6 \
                            size    54910
}

# Optional runtime dependency to use the net.http and net.websocket modules
depends_run         path:lib/libssl.dylib:openssl

depends_lib         port:boehmgc

# https://trac.macports.org/ticket/64913
if {${build_arch} in "ppc ppc64"} {
    configure.ldflags-append -latomic
}

# Path to V compiler
set vc_path ${workpath}/vc-${vc_commit}

# ./thirdparty/stdatomic/nix/atomic.h:12:10: fatal error: 'stdatomic.h' file not found
# Based on cmus port: compatible compilers are Xcode > Xcode 7, clang > 3.7, and gcc > 4.8
compiler.c_standard 2011
compiler.blacklist-append {clang < 700} macports-clang-3.*

# Stop V from looking in Homebrew directories, and remove hardcoded MacPorts prefix.
# V has built-in font rendering, but users can optionally use freetype.
patchfiles          patch-boehmgc-prefix.diff \
                    patch-freetype2-prefix.diff \
                    patch-openssl-prefix.diff \
                    patch-pkgconfig-prefix.diff \
                    patch-postgresql-prefix.diff \
                    patch-makefile.diff

# See https://github.com/vlang/v/issues/6605
# Header file <sys/errno.h>, needed for module `sync` was not found
if { ${os.platform} eq "darwin" && ${os.major} <= 9 } {
    patchfiles-append patch-errno-header.diff
}

post-patch {
    # https://github.com/vlang/v/issues/10796 - Set correct deployment target.
    foreach f [list ${worksrcpath}/vlib/v/builder/cc.v ${vc_path}/v.c] {
        reinplace "s|-mmacosx-version-min=10.7|-mmacosx-version-min=${macosx_deployment_target}|" ${f}
    }

    # Disable vlang self update feature.
    copy -force ${filespath}/vup.v ${worksrcpath}/cmd/tools

    foreach f [list net/openssl/c.v fontstash/a_d_use_freetype.v pg/pg.v v/pkgconfig/pkgconfig.v builtin/builtin_d_gcboehm.c.v] {
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" vlib/${f}
    }

    if { ${os.platform} eq "darwin" && ${os.major} <= ${legacy_version} } {
        file mkdir ${worksrcpath}/thirdparty/legacy
        move ${workpath}/macports-legacy-support-${legacysupport_version} ${worksrcpath}/thirdparty/legacy/source
    }
}

# -cg, -showcc and -show-c-output allows for better error debugging
# local prevents fetching the V compiler during the build
build.args-append   local=true \
                    VFLAGS="-v -cg -showcc -show-c-output -prod -cc ${configure.cc} -cflags [shellescape ${configure.cflags}]" \
                    VC=${vc_path}

# See https://trac.macports.org/ticket/64913
# In case _developer group doesn't exist
add_users vlang group=_developer

destroot {
    set library_path ${destroot}${prefix}/lib/${name}
    set examples_path ${destroot}${prefix}/share/examples

    xinstall -d ${examples_path}
    move ${worksrcpath}/examples ${examples_path}/${name}

    xinstall -d ${library_path}
    foreach f {cmd thirdparty v v.mod vlib} {
        move ${worksrcpath}/${f} ${library_path}/${f}
    }

    ln -s ${prefix}/lib/${name}/v ${destroot}${prefix}/bin

    # Vlang compiles the subcommands in the tools dir on first runtime usage (e.g. v doctor).
    # Allow running Vlang to those in the _developer group, rather than running sudo each time.
    # See https://github.com/vlang/v/issues/10324

    # Majority of subcommands only require ${library_path}/cmd/tools to be writable but v self requires
    # all of ${library_path}

    fs-traverse item ${library_path} {
        file attributes ${item} -group _developer -permissions g+w
    }
}

# Delete leftover subcommands compiled during runtime
post-deactivate {
    delete ${prefix}/lib/${name}
}

# Based on various Android ports - don't make files world-writable.
# e.g. https://github.com/macports/macports-ports/blob/master/java/android/Portfile
notes "
The Vlang library directory is group _developer writable. You need to be a member of the
_developer group to use Vlang. If you are not, run:

sudo dscl . append /Groups/_developer GroupMembership <username>

Optionally, to use the V PostgreSQL module, install PostgreSQL and set the POSTGRESQL_VERSION
environment variable. For PostgreSQL 14:

export POSTGRESQL_VERSION=14
"

# Check for weekly versions only
livecheck.version ${version}
livecheck.regex weekly.(\[0-9.\]+\[0-9\])
